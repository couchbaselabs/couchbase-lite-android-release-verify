plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
}

android {
    namespace 'com.example.cblverify'

    compileSdk {
        version = release(36)
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    defaultConfig {
        applicationId "com.example.cblverify"
        minSdk 24
        targetSdk 36
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // Add flavors to test core vs ktx in same project
    flavorDimensions += "cbl"
    productFlavors {
        core {
            dimension "cbl"
            buildConfigField "String", "CBL_FLAVOR", "\"core\""
        }
        ktx {
            dimension "cbl"
            buildConfigField "String", "CBL_FLAVOR", "\"ktx\""
        }
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}


def cblVersion = (findProperty("cblVersion") ?: "4.0.2").toString()
def cblCoreArtifact = (findProperty("cblCoreArtifact") ?: "couchbase-lite-android-ee").toString()
def cblKtxArtifact  = (findProperty("cblKtxArtifact")  ?: "couchbase-lite-android-ee-ktx").toString()

dependencies {
    implementation libs.appcompat
    implementation libs.material

    // Flavor-specific Couchbase deps:
    coreImplementation "com.couchbase.lite:${cblCoreArtifact}:${cblVersion}"
    ktxImplementation  "com.couchbase.lite:${cblKtxArtifact}:${cblVersion}"

    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}
